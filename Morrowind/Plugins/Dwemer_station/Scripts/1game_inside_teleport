begin 1game_inside_teleport

;Поднимает платформу наверх и позволяет игроку телепортироваться по станции

float timer
short ready
short doonce
 
if ( MenuMode == 1 )
  return
endif
 
if ( OnActivate == 1 )
  if ( ready == 0 )
    if ( "1game_energy" == 0 )
      messagebox "Ничего не происходит, кажется нет питания"
      return
    elseif ( "1game_energy" == 1 )
      set ready to 1
      return
    endif
  endif
endif
 
if ( ready == 1 )
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set "1game_ready" to 1
      PlayLoopSound3DVP "BM pipe medium", 1.0, 1.0
      set doonce to 1
    endif
    move z -140
    rotate z 180
  elseif ( timer < 3 )
    set timer to 0
    set ready to 2
    set doonce to 0
    messagebox "Готов. Для телепортации встаньте на платформу"
  endif
endif
 
if ( ready == 2 )
  set timer to ( timer + GetSecondsPassed )
  if (timer > 30 )
    set timer to 0
    set ready to 3
  endif
endif

if ( ready == 3 )
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set "1game_ready" to 0
      PlayLoopSound3DVP "BM pipe large", 1.0, 1.0
      set doonce to 1
    endif
    move z 140
    rotate z -180
  elseif ( timer < 3 )
    set timer to 0
    set ready to 0
    set doonce to 0
  endif
endif
 
end
