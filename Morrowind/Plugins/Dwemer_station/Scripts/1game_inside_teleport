begin 1game_inside_teleport

;Поднимает платформу наверх и позволяет игроку телепортироваться по станции

float timer
short ready
short doonce

if ( MenuMode == 1 )
  return
endif

if ( GetStandingPC == 1 );Если игроку захочется залезть на платформу, её заклинит
 if ( ready > 0 )
  return
 endif
endif

if ( "1game_get_teleported" == 1 );данный блок должен поднимать панель наверх, после телепортации
    move z 140;и начать опускать вниз сразу, когда игрок сойдет с платформы
  if ( "1game_get_out" == 1 )
    set ready to 3
    set "1game_get_out" to 0
    set "1game_get_teleported" to 0
  endif
endif

if ( OnActivate == 1 );Проверяет если есть поток электричества и запускает подьем
  if ( ready == 0 )
    if ( "1game_energy" == 0 )
      messagebox "Ничего не происходит, кажется нет питания"
      return
    elseif ( "1game_energy" == 1 )
      set ready to 1
      return
    endif
  endif
endif

if ( ready == 1 );Поднимает панель вверх для доступа на платформу
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set doonce to 1
      set "1game_ready" to 1
      playsound "BM pipe large"
    endif
    move z -140
    rotate z 180
  elseif ( timer < 3 )
    set timer to 0
    set ready to 2
    set doonce to 0
    set "1game_ready" to 2
    messagebox "Готов. Для телепортации встаньте на платформу"
  endif
endif
 
if ( ready == 2 );Ждет 30 секунд перед закрытием платформы
  set timer to ( timer + GetSecondsPassed )
  if (timer > 30 )
    set timer to 0
    set ready to 3
  endif
endif

if ( ready == 3 );Начинает опускать платформу
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set "1game_ready" to 0
      set doonce to 1
      playsound "BM pipe medium"
    endif
    move z 140
    rotate z -180
  elseif ( timer < 3 )
    set timer to 0
    set ready to 0
    set doonce to 0
  endif
endif
 
end
