begin 1game_inside_teleport

float timer
short ready
short doonce

if ( MenuMode == 1 )
  return
endif

if ( "1game_get_teleported" == 1 )
  if ( GetPCCell, "Гелендорф, Башня (Мастерская)" == 1 )
    setpos z "12413"
  elseif ( GetPCCell, "Гелендорф, Зал Ветров (Жизнеобеспечение)" == 1 )
    setpos z "12426"
  elseif ( GetPCCell, "Гелендорф, Зал Равновесия (Хол)" == 1 )
    setpos z "225"
  elseif ( GetPCCell, "Гелендорф, Зал Телепортации" == 1 )
    setpos z "15245"
  elseif ( GetPCCell, "Гелендорф, Нижний Уровень (Энергообеспечение)" == 1 )
    setpos z "545"
  endif
  set timer to 0
  set doonce to 0
  set "1game_ready" to 2
  set ready to 2
  set "1game_get_teleported" to 0
endif
  

if ( ready == 2 )
  set timer to ( timer + GetSecondsPassed )
  if (timer > 30 )
    set timer to 0
    set ready to 3
  endif
endif

if ( GetStandingPC == 1 )
  return
endif

if ( OnActivate == 1 )
  if ( ready == 0 )
    if ( "1game_energy" == 0 )
      messagebox "Ничего не происходит, кажется, нет питания"
      return
    elseif ( "1game_energy" == 1 )
      set ready to 1
    endif
  endif
endif

if ( ready == 1 )
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set doonce to 1
      set "1game_ready" to 1
      playsound "BM pipe large"
    endif
    move z -140
    rotate z 180
  elseif ( timer < 3 )
    set timer to 0
    set doonce to 0
    set "1game_ready" to 2
    set ready to 2
    messagebox "Готов. Для телепортации встаньте на платформу"
  endif
endif

if ( ready == 3 )
  set timer to ( timer + GetSecondsPassed )
  if ( timer < 2 )
    if ( doonce == 0 )
      set "1game_ready" to 0
      set doonce to 1
      playsound "BM pipe medium"
    endif
    move z 140
    rotate z -180
  elseif ( timer < 3 )
    set timer to 0
    set ready to 0
    set doonce to 0
  endif
endif
 
end
